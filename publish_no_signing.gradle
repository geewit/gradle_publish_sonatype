import java.nio.charset.StandardCharsets

apply plugin: 'java'
apply plugin: 'maven-publish'

buildDir = 'target'

repositories {
    mavenLocal()
    maven { url "https://maven.aliyun.com/repository/central" }
    maven { url "http://repo1.maven.org/maven2" }
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

//设置编码
compileJava.options*.compilerArgs << "-Xlint:unchecked" << "-Xlint:options"

[compileJava, javadoc]*.options*.encoding = StandardCharsets.UTF_8.name()

def isSnapshotBuild() {
    return version.endsWith('SNAPSHOT')
}

def getReleaseRepositoryUrl() {
    def RELEASE_REPOSITORY_URL_KEY = hasProperty('RELEASE_REPOSITORY_URL_KEY') ? getProperty('RELEASE_REPOSITORY_URL_KEY') : 'RELEASE_REPOSITORY_URL'
    def releaseRepositoryUrl = hasProperty(RELEASE_REPOSITORY_URL_KEY) ? getProperty(RELEASE_REPOSITORY_URL_KEY)
            : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
    println "releaseRepositoryUrl:" + releaseRepositoryUrl
    return releaseRepositoryUrl
}

def getSnapshotRepositoryUrl() {
    def SNAPSHOT_REPOSITORY_URL_KEY = hasProperty('SNAPSHOT_REPOSITORY_URL_KEY') ? getProperty('SNAPSHOT_REPOSITORY_URL_KEY') : 'SNAPSHOT_REPOSITORY_URL'
    def snapshotRepositoryUrl =  hasProperty(SNAPSHOT_REPOSITORY_URL_KEY) ? getProperty(SNAPSHOT_REPOSITORY_URL_KEY)
            : "https://oss.sonatype.org/content/repositories/snapshots/"
    println "snapshotRepositoryUrl:" + snapshotRepositoryUrl
    return snapshotRepositoryUrl
}

def getRepositoryUsername() {
    def SONATYPE_USERNAME_KEY = hasProperty('SONATYPE_USERNAME_KEY') ? getProperty('SONATYPE_USERNAME_KEY') : 'SONATYPE_USERNAME'
    def username = hasProperty(SONATYPE_USERNAME_KEY) ? getProperty(SONATYPE_USERNAME_KEY) : ""
    println "username:" + username
    return username
}

def getRepositoryPassword() {
    def SONATYPE_PASSWORD_KEY = hasProperty('SONATYPE_PASSWORD_KEY') ? getProperty('SONATYPE_PASSWORD_KEY') : 'SONATYPE_PASSWORD'
    def password = hasProperty(SONATYPE_PASSWORD_KEY) ? getProperty(SONATYPE_PASSWORD_KEY) : ""
    println "password:" + password
    return password
}


task sources(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sources
}

// https://docs.gradle.org/current/dsl/org.gradle.api.publish.maven.MavenPublication.html
publishing {
    publications {
        maven(MavenPublication) {
            groupId project.group
            artifactId project.name
            version "${version}"
            from components.java
            artifact sources

            pom {
                name = POM_ARTIFACT_ID
                description = POM_DESCRIPTION
                url = POM_SCM_URL
                developers {
                    developer {
                        id = POM_DEVELOPER_ID
                        name = POM_DEVELOPER_NAME
                        email = POM_DEVELOPER_EMAIL
                    }
                }
                scm {
                    url = POM_SCM_URL
                    connection = POM_SCM_CONNECTION
                    developerConnection = POM_SCM_DEV_CONNECTION
                }
            }
        }
    }
    repositories {
        maven {
            name = 'nexusRepository'
            url = isSnapshotBuild() ? getSnapshotRepositoryUrl() : getReleaseRepositoryUrl()
            credentials {
                username = getRepositoryUsername()
                password = getRepositoryPassword()
            }
        }
    }
}