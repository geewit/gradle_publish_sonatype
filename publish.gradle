import groovy.json.JsonSlurper
import java.nio.charset.StandardCharsets

ext {
    profile = System.getProperty("profile") ?: "development"
    println "profile = ${profile}"
}

Properties props = new Properties()
def versionFile = rootProject.file('version.properties')
println "rootProject: ${rootProject.name}"

if(versionFile.exists()) {
    props.load(versionFile.newReader())
    props.each { prop ->
        println "${prop.key}: ${prop.value}"
        rootProject.ext.set(prop.key, prop.value)
    }
}
def excludeArtifacts = []
def excludeFile = rootProject.file('gradle-exclude.json')
if(excludeFile.exists()) {
    excludeArtifacts = new JsonSlurper().parseText(excludeFile.text)
    excludeArtifacts?.each { item ->
        println "exclude ${item.group}: ${item.module}"
    }
}

def publish_need_sign = false
if (rootProject.hasProperty('publish_need_sign')) {
    publish_need_sign = 'true'.equalsIgnoreCase(rootProject.getProperties().get('publish_need_sign').toString())
}

def java_version = JavaVersion.VERSION_1_8
if (rootProject.hasProperty('geewit.gradle.java_version')) {
    java_version = JavaVersion.toVersion(rootProject.getProperties().get('geewit.gradle.java_version'))
}
println "java_version: ${java_version}"

buildscript {
    repositories {
        mavenLocal()
        maven { url "https://maven.aliyun.com/repository/gradle-plugin" }
        maven { url "https://repo.maven.apache.org/maven2" }
    }
}

apply plugin: 'java-library'
apply plugin: 'maven-publish'
if (publish_need_sign) {
    apply plugin: 'signing'
}

buildDir = 'target'

repositories {
    mavenLocal()
    maven {
        url getReleaseRepositoryUrl(rootProject)
        allowInsecureProtocol true
        credentials {
            username = getRepositoryUsername(rootProject)
            password = getRepositoryPassword(rootProject)
        }
    }
    maven {
        url getSnapshotRepositoryUrl(rootProject)
        allowInsecureProtocol true
        credentials {
            username = getRepositoryUsername(rootProject)
            password = getRepositoryPassword(rootProject)
        }
    }
    maven {
        name "aliyun"
        url "https://maven.aliyun.com/repository/central"
    }
    maven {
        name "central"
        url "https://repo.maven.apache.org/maven2"
    }
}

task sources(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task javadocs(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(JavaCompile) {
    sourceCompatibility = java_version
    targetCompatibility = java_version

    options*.compilerArgs << "-Xlint:unchecked" << "-Xlint:options"
    options*.encoding = StandardCharsets.UTF_8.name()
}

tasks.withType(Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(Javadoc) {
    failOnError = false
    options.windowTitle("${project.name} Javadoc ${project.version}")
    options*.encoding = StandardCharsets.UTF_8.name()
    options.addStringOption('Xdoclint:none', '-quiet')
}

tasks.withType(Copy) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

static def groupId(Project thisProject, Project rootProject) {
    println "thisProject.name = ${thisProject.name}"
    def prj = thisProject.parent
    def groupId = null
    Properties projectProps = new Properties()
    def projectPropsFile = thisProject.file('gradle.properties')
    if(projectPropsFile.exists()) {
        projectProps.load(projectPropsFile.newReader())
        groupId = projectProps.getProperty('group')
    }
    if(groupId == null) {
        println "project has not groupId"
        groupId = rootProject.group
        println "groupId = ${groupId}"
        if(thisProject.parent != null) {
            def deepth = 1
            println "thisProject.name = ${thisProject.name}"
            def group_suffix = ""
            while (prj != null) {
                println "deepth = ${deepth}"
                if(prj.parent != null) {
                    if(group_suffix.length() > 0) {
                        group_suffix = prj.name + "." + group_suffix
                    } else {
                        group_suffix = prj.name
                    }
                }
                println "group_suffix = ${group_suffix}"
                prj = prj.parent
                deepth++
            }
            if(group_suffix.length() > 0) {
                groupId = groupId + "." + group_suffix
            }
        } else {
            println "thisProject.parent == null"
            groupId = thisProject.group
            println "groupId = ${groupId}"
        }
    }
    println "--------- final groupId = ${groupId}"
    thisProject.group(groupId)
    return groupId
}

static def artifactId(Project project) {
    def prj = project.parent

    def artifactId
    if(project.hasProperty("artifactId")) {
        artifactId = project.property("artifactId")
        println "project.artifactId = ${artifactId}"
    } else {
        println "project has not artifactId"
        artifactId = project.name
        println "artifactId = ${artifactId}"
        while (prj != null) {
            println "prj.name = ${prj.name}"
            println "prj.depth = ${prj.depth}"
            artifactId = "${prj.name}-${artifactId}"
            println "artifactId = ${artifactId}"
            prj = prj.parent
        }
    }
    println "--------- final artifactId = ${artifactId}"
    return artifactId
}

static def isSnapshotBuild(Project rootProject) {
    return rootProject.version.contains("SNAPSHOT")
}

static def getRepositoryUrl(Project rootProject) {
    return isSnapshotBuild(rootProject) ? getSnapshotRepositoryUrl(rootProject) : getReleaseRepositoryUrl(rootProject)
}

static def getReleaseRepositoryUrl(Project rootProject) {
    def property_default_key = 'REPOSITORY_SONATYP_ERELEASE_URL'
    def property_key = 'REPOSITORY_RELEASE_URL_KEY'
    def RELEASE_REPOSITORY_URL_KEY = rootProject.hasProperty(property_key) ? rootProject.getProperty(property_key) : property_default_key
    def releaseRepositoryUrl = rootProject.hasProperty(RELEASE_REPOSITORY_URL_KEY) ? rootProject.getProperty(RELEASE_REPOSITORY_URL_KEY)
            : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
    println "releaseRepositoryUrl:" + releaseRepositoryUrl
    return releaseRepositoryUrl
}

static def getSnapshotRepositoryUrl(Project rootProject) {
    def property_default_key = 'REPOSITORY_SONATYP_SNAPSHOT_URL'
    def property_key = 'REPOSITORY_SNAPSHOT_URL_KEY'
    def SNAPSHOT_REPOSITORY_URL_KEY = rootProject.hasProperty(property_key) ? rootProject.getProperty(property_key) : property_default_key
    def snapshotRepositoryUrl =  rootProject.hasProperty(SNAPSHOT_REPOSITORY_URL_KEY) ? rootProject.getProperty(SNAPSHOT_REPOSITORY_URL_KEY)
            : "https://oss.sonatype.org/content/repositories/snapshots/"
    println "snapshotRepositoryUrl:" + snapshotRepositoryUrl
    return snapshotRepositoryUrl
}

static def getRepositoryUsername(Project rootProject) {
    if(rootProject.ext.has('REPOSITORY_USERNAME')) {
        println "rootProject.ext.get('REPOSITORY_USERNAME'): " + rootProject.ext.get('REPOSITORY_USERNAME')
        return rootProject.ext.get('REPOSITORY_USERNAME')
    }
    def property_default_key = 'REPOSITORY_USERNAME'
    def property_key = 'REPOSITORY_USERNAME_KEY'
    def SONATYPE_USERNAME_KEY = rootProject.hasProperty(property_key) ? rootProject.getProperty(property_key) : property_default_key
    def username = rootProject.hasProperty(SONATYPE_USERNAME_KEY) ? rootProject.getProperty(SONATYPE_USERNAME_KEY) : ""
    println "username: + $username"
    rootProject.ext.set('REPOSITORY_USERNAME', username)
    return username
}

static def getRepositoryPassword(Project rootProject) {
    if(rootProject.ext.has('REPOSITORY_PASSWORD')) {
        println "rootProject.ext.get('REPOSITORY_PASSWORD'): " + rootProject.ext.get('REPOSITORY_PASSWORD')
        return rootProject.ext.get('REPOSITORY_PASSWORD')
    }
    def property_default_key = 'REPOSITORY_PASSWORD'
    def property_key = 'REPOSITORY_PASSWORD_KEY'
    def SONATYPE_PASSWORD_KEY = rootProject.hasProperty(property_key) ? rootProject.getProperty(property_key) : property_default_key
    def password = rootProject.hasProperty(SONATYPE_PASSWORD_KEY) ? rootProject.getProperty(SONATYPE_PASSWORD_KEY) : ""
    println "password:" + password
    rootProject.ext.set('REPOSITORY_PASSWORD', password)
    return password
}

// https://docs.gradle.org/current/dsl/org.gradle.api.publish.maven.MavenPublication.html
publishing {
    publications {
        api(MavenPublication) {
            afterEvaluate {
                def thisGroupId = groupId(project, rootProject)
                def thisArtifactId = artifactId(project)
                groupId thisGroupId
                artifactId thisArtifactId
                version version
                println "api artifact: ${thisGroupId}:${thisArtifactId}:${version}"
                artifact apiJar
                artifact javadocs
                if(publishSources) {
                    println "publishing.artifact sources"
                    artifact sources
                }
            }
            pom {
                description = POM_DESCRIPTION
            }
        }
        maven(MavenPublication) {
            afterEvaluate {
                def thisGroupId = groupId(project, rootProject)
                def thisArtifactId = artifactId(project)
                groupId thisGroupId
                artifactId thisArtifactId
                version version
                println "artifact: ${thisGroupId}:${thisArtifactId}:${version}"
                from components.java
                println "publishing.publishSources: ${publishSources}"
                if(publishSources) {
                    println "publishing.artifact sources"
                    artifact sources
                }
            }
            pom {
                name = POM_ARTIFACT_ID
                description = POM_DESCRIPTION
                url = POM_SCM_URL
                licenses {
                    license {
                        name = POM_LICENCE_NAME
                        url = POM_LICENCE_URL
                    }
                }
                developers {
                    developer {
                        id = POM_DEVELOPER_ID
                        name = POM_DEVELOPER_NAME
                        email = POM_DEVELOPER_EMAIL
                    }
                }
                scm {
                    url = POM_SCM_URL
                    connection = POM_SCM_CONNECTION
                    developerConnection = POM_SCM_DEV_CONNECTION
                }
            }
        }
    }
    repositories {
        maven {
            name = 'releaseRepository'
            url = getRepositoryUrl(rootProject)
            allowInsecureProtocol true
            credentials {
                username = getRepositoryUsername(rootProject)
                password = getRepositoryPassword(rootProject)
            }
        }
    }
}

if (publish_need_sign) {
    signing {
        sign publishing.publications.maven
    }
}
